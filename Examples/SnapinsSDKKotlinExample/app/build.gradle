/*
 * Copyright (c) 2018, salesforce.com, inc.
 * All rights reserved.
 * Licensed under the BSD 3-Clause license.
 * For full license text, see LICENSE file in the repo root or https://opensource.org/licenses/BSD-3-Clause
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    signingConfigs {
        defaultSigningConfig {
            // FIXME publish a public default signing configuration?
            storeFile file(project.service_sdk_keystore)
            storePassword project.service_sdk_keystore_password
            keyAlias project.service_sdk_key_alias
            keyPassword project.service_sdk_key_password
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.salesforce.snapinssdkexample"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        kotlinImplDebug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.defaultSigningConfig
        }
        javaImplDebug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.defaultSigningConfig
        }
    }

    sourceSets {
        kotlinImplDebug {
            res.srcDirs = ['main/res'] // FIXME define in one place?
        }
        javaImplDebug {
            res.srcDirs = ['main/res'] // FIXME define in one place?
        }
    }

    // Ignore debug and release variants to favor use of kotlinImplDebug and javaImplDebug
    variantFilter { variant ->
        if (variant.buildType.name.equals('release') || variant.buildType.name.equals('debug')) {
            variant.setIgnore(true)
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    // Android
    implementation "com.android.support:appcompat-v7:$appCompatV7SupportVersion"
    // FAB Speeddial
    implementation "io.github.yavski:fab-speed-dial:$fab_speed_dial_version"
    // Salesforce SDKs
    implementation "com.salesforce.service:servicesdk:${rootProject.ext.serviceSdkVersion}"
    implementation "com.salesforce.mobilesdk:SalesforceSDK:${rootProject.ext.mobilesdkVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
}
